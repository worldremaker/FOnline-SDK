// Author: cvet
// Script attached to all graves, in protos.
// Graves pids: 344..347, 374..376.
#include "_macros.fos"
#include "_itempid.fos"
#include "_msgstr.fos"

import void SwitchState( Item& locker ) from "lockers";


void _GraveInit( Item& grave, bool firstTime )
{
    grave.SetEvent( ITEM_EVENT_SKILL, "_GraveSkill" );
    grave.SetEvent( ITEM_EVENT_USE_ON_ME, "_GraveUseOnMe" );
}

bool _GraveSkill( Item& grave, Critter& cr, int skill )
{
    if( skill == SKILL_PICK_ON_GROUND || skill == SK_LOCKPICK )
    {
        if( FLAG( grave.LockerCondition, LOCKER_ISOPEN ) )
            cr.ShowContainer( null, grave, TRANSFER_HEX_CONT_DOWN );
        else
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_USE_NOTHING );
        return true;
    }
    return false;
}

bool _GraveUseOnMe( Item& grave, Critter& cr, Item@ usedItem )
{
    if( valid( usedItem ) && usedItem.GetProtoId() == PID_SHOVEL )
    {
        SwitchState( grave );
        if( FLAG( grave.LockerCondition, LOCKER_ISOPEN ) )
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_GRAVE_UNCOVERED );
            cr.StatBase[ ST_KARMA ] -= 5;
            cr.KarmaBase[ KARMA_GRAVE_DIGGER ] = 1;
        }
        else
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_GRAVE_COVERED );
        }
        return true;
    }
    return false;
}
